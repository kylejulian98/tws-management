import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = "dev.kylejulian.twsmanagement"

if (hasProperty('ver')) {
    version = ver.substring(1)
} else {
    SimpleDateFormat sdf = new SimpleDateFormat("yyMMdd-HHmm")
    sdf.setTimeZone(TimeZone.getTimeZone("UTC"))
    version = sdf.format(new Date()).toString()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()

    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
  	maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  	maven {
        url 'https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core'
    }
  	maven {
        url 'https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2'
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.2'
    implementation 'org.apache.commons:commons-dbcp2:2.7.0' // Connection Pooling
    
    compileOnly files('lib/TABv2.8.4.jar')
    compileOnly 'net.luckperms:api:5.0'
    compileOnly 'org.jetbrains:annotations:19.0.0'
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.2-R0.1-SNAPSHOT'
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( VERSION: version, PACKAGE: project.group.toString(), NAME: rootProject.name )
    }
}

shadowJar {
    archiveClassifier.set('SNAPSHOT')
    relocate 'io.papermc.lib', 'shadow.io.papermc.lib'
}

jar.enabled = false
assemble.dependsOn(shadowJar)

task release {
    dependsOn build
    doLast {
        def dirStr = buildDir.toString() + '/libs/'
        file(dirStr + rootProject.name + '-' + version + '-SNAPSHOT.jar').renameTo(dirStr + rootProject.name + '.jar')
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/kylejulian98/tws.management')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('TOKEN')
            }
        }
    }

    publications {
        shadow(MavenPublication) { publication ->
            version = version
            project.shadow.component(publication)
        }
    }
}